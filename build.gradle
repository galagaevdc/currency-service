plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.diffplug.spotless' version '6.22.0'
	id 'checkstyle'
	id 'jacoco'
}

group = 'com.scribe'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

spotless {
	lineEndings 'UNIX'
	java {
		target 'src/*/java/**/*.java'
		importOrder()
		removeUnusedImports()
		googleJavaFormat()
	}
}

tasks.named('check') {
	dependsOn 'spotlessCheck'
}

jacocoTestReport {
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: ['**/**Application.class','**/dto/**', '**/response/**', '**/entity/**'])
		}))
	}
}

repositories {
	mavenCentral()
}

ext {
	testContainersVersion = '1.20.0'
	lombokVersion = '1.18.34'
	mapstructVersion = '1.5.5.Final'
	springDocVersion = '2.6.0'
	wiremockTestContainerVersion = '1.0-alpha-13'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
	implementation "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
	implementation 'org.liquibase:liquibase-core'
	runtimeOnly 'org.postgresql:postgresql'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation "org.testcontainers:testcontainers:${testContainersVersion}"
	testImplementation "org.testcontainers:postgresql:${testContainersVersion}"
	testImplementation "org.testcontainers:junit-jupiter:${testContainersVersion}"
	testImplementation "org.wiremock.integrations.testcontainers:wiremock-testcontainers-module:${wiremockTestContainerVersion}"
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}
